Option Explicit

Sub HighlightWords()

    Dim strSearch As String
    Dim trimmedWord As String
    Dim i As Long
    Dim regEx As New RegExp
    Dim matches As Object
    Dim match As Object
    Dim pattern As String
    Dim start As Long
    Dim length As Long
    Dim highlightRange As Range
    Dim highlightColor As WdColorIndex
    Dim fd As FileDialog
    Dim filePath As Variant
    Dim fileContent As String
    Dim words() As String
    Dim word As Variant
    Dim totalIndividualMatchCount As Long
    Dim totalMatchCount As Long
    Dim individualMatchCount As Long
    Dim individualMsg As String
    Dim resultMsg As String
    Dim docName As String
    Dim resultFilePath As String
    
    ' Kullaniciya dosya seçtirme islemi
    Set fd = Application.FileDialog(msoFileDialogFilePicker)
    With fd
        .Title = "Select the file(s) containing the words to highlight"
        .Filters.Clear
        .Filters.Add "Text Files", "*.txt"
        .AllowMultiSelect = True
        If .Show = True Then
            ' Dosya içerigini birlestirme
            For Each filePath In .SelectedItems
                'Dosya icerigini okuma
                Open filePath For Input As #1
                fileContent = fileContent & Input$(LOF(1), #1)
                Close #1
                Next filePath
        Else
            Exit Sub
        End If
    End With
    
    ' Dosya içerigini okuma
    'Open filePath For Input As #1
    'fileContent = Input$(LOF(1), #1)
    'Close #1
    
    words = Split(fileContent, ",")
    ' Arama patternini olusturma
    For i = LBound(words) To UBound(words)
        trimmedWord = LTrim(RTrim(words(i)))
        strSearch = trimmedWord
        'strSearch = "bayrak"
        If strSearch <> "" Then
            ' Türkçe karakterleri kapsayan bir arama patterni olusturma
            strSearch = Replace(strSearch, "i", "[i" & ChrW(305) & "]")
            strSearch = Replace(strSearch, "I", "[I" & ChrW(304) & "]")
            strSearch = Replace(strSearch, "u", "[uü]")
            strSearch = Replace(strSearch, "U", "[UÜ]")
            strSearch = Replace(strSearch, "o", "[oö]")
            strSearch = Replace(strSearch, "O", "[OÖ]")
            strSearch = Replace(strSearch, "c", "[cç]")
            strSearch = Replace(strSearch, "C", "[CÇ]")
            strSearch = Replace(strSearch, "g", "[g" & ChrW(287) & "]")
            strSearch = Replace(strSearch, "G", "[G" & ChrW(286) & "]")
            strSearch = Replace(strSearch, "s", "[s" & ChrW(351) & "]")
            strSearch = Replace(strSearch, "S", "[S" & ChrW(350) & "]")

        
        ' Son harfi p, ç, t veya k ise ünsüz yumusamasina ugrar, bu harfler için ayri bir kontrol yapilir
            If Right(strSearch, 1) = "p" Then
                strSearch = Left(strSearch, Len(strSearch) - 1) & "[pb]"
            ElseIf Right(strSearch, 1) = ChrW(231) Then
                strSearch = Left(strSearch, Len(strSearch) - 1) & "[c" & ChrW(231) & "]"
            ElseIf Right(strSearch, 1) = "t" Then
                strSearch = Left(strSearch, Len(strSearch) - 1) & "[td]"
            ElseIf Right(strSearch, 1) = "k" Then
                strSearch = Left(strSearch, Len(strSearch) - 1) & "[k" & "g" & ChrW(287) & "]"
            End If
                pattern = "\b\w*" & strSearch & "\w*\b" ' Arama patterni, kelimenin gectigi yerlerle eslesecek sekilde düzenlendi.
                
            ' Ilgili kelime için regEx objesi olusturma
            regEx.pattern = pattern
            regEx.IgnoreCase = True ' Büyük/küçük harf duyarliligi yoktur.
            regEx.Global = True
            ' Ilgili kelimeyi belirginlestirmek için eslesmeleri bulma
            Set matches = regEx.Execute(ActiveDocument.Range.Text)
            individualMatchCount = matches.Count
            
            If individualMatchCount <> 0 Then
                totalIndividualMatchCount = totalIndividualMatchCount + 1
                totalMatchCount = totalMatchCount + individualMatchCount
                individualMsg = individualMsg & trimmedWord & ": " & individualMatchCount & vbNewLine
            End If
            ' Eslesmeleri belirginlestir
            For Each match In matches
                
                start = match.FirstIndex
                length = match.length
                Set highlightRange = ActiveDocument.Range(start, start + length)
                highlightColor = highlightRange.HighlightColorIndex
        
                If highlightColor = wdYellow Then
                    highlightRange.HighlightColorIndex = wdNoHighlight
                Else
                    highlightRange.HighlightColorIndex = wdYellow
                End If
            Next
        End If
    Next i
    
    ' Mesaji olustur
    resultMsg = individualMsg & "Toplam tekil kelime: " & totalIndividualMatchCount & vbNewLine
    resultMsg = resultMsg & "Toplam kelime: " & totalMatchCount
    
    MsgBox resultMsg
    
    ' Sonuclari dosyaya yazma
    docName = Left(ActiveDocument.Name, InStrRev(ActiveDocument.Name, ".") - 1)

    resultFilePath = docName & "_search_results.txt"
    Open resultFilePath For Output As #2
    Print #2, resultMsg
    Close #2


End Sub
